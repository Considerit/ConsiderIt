@ConsiderIt.module "Franklin.Point", (Point, App, Backbone, Marionette, $, _) ->
  
  # High level point view and two subclasses. Each point is generated by a points controller. 
  class Point.PointView extends App.Views.Layout
    actions : [] # the point actions available for this class of Point

    tagName : 'li'
    template : '#tpl_point_layout'

    regions :
      headerRegion : '.point_avatar_region'
      bodyRegion : '.point_summary_region'
      openPointRegion : '.open_point_region'

    serializeData : ->
      params = _.extend {}, @model.attributes, 
        actions : @actions

      params

    @events : 
      'click .close_open_point' : 'closePoint'
      'click .point_content' : 'pointClicked'

    pointClicked : (ev) ->
      pass_through = @$el.parents('.points_by_community[state="summary"]').length > 0
      _.each App.request('shared:actions'), (target) ->
        pass_through ||= $(ev.target).is("[action='#{target}']")

      if !pass_through
        @trigger 'point:clicked'
        ev.stopPropagation()

    closePoint : (ev) ->
      # only for closing points
      $('#l_wrap').trigger 'click'
      ev.stopPropagation()

  class Point.CommunityPointView extends Point.PointView
    actions : ['include']

    events : _.extend @events,
      'click [action="point-include"]' : 'includePoint'
      'mouseenter .point_content' : 'highlightIncluders'
      'mouseleave .point_content' : 'unhighlightIncluders'
      
    includePoint : (ev) ->
      @trigger 'point:include'
      ev.stopPropagation()

    highlightIncluders : ->
      @trigger 'point:highlight_includers'

    unhighlightIncluders : ->
      @trigger 'point:unhighlight_includers'

    disableDrag : ->
      try
        @$el.find('.point_content').draggable 'destroy'
      catch e
        # get here when nav to results page before draggable created

    enableDrag : ->
      @$el.find('.point_content').draggable
        revert: "invalid"


  class Point.DecisionBoardPointView extends Point.PointView
    actions : ['remove']

    events : _.extend @events,
      'click [action="point-remove"]' : 'removePoint'

    removePoint : (ev) ->
      @trigger 'point:remove'
      ev.stopPropagation()



  # View that manages the content shown when a point is opened.
  class Point.OpenPointView extends App.Views.Layout
    template : '#tpl_open_point'
    className : 'open_point_layout'
    regions :
      followRegion : '.point_follow_region'
      assessmentRegion : '.point_assessment_region'
      discussionRegion : '.point_discussion_region'

    serializeData : ->
      _.extend {}, @model.attributes, 
        actions : @options.actions

    onRender : ->
      App.vent.trigger 'point:opened'

    onShow : ->
      # when clicking outside of point, close it      
      $(document).on 'click.close_point_event', (ev)  => 
        is_not_clicking_this_point = ($(ev.target).closest('.open_point').length == 0 || $(ev.target).closest('.open_point').data('id') != @model.id)
        dialog_not_open = $('.l-dialog-detachable').length == 0
        if is_not_clicking_this_point && $(ev.target).closest('.editable-buttons').length == 0 && dialog_not_open
          is_click_within_a_point = $(ev.target).closest('[role="point"]').length > 0
          is_clicking_nav = $(ev.target).closest('.l-navigate-wrap').length > 0
          @closePoint( !is_click_within_a_point && !is_clicking_nav ) 

      $(document).on 'keyup.close_point_event', (ev) => 
        dialog_not_open = $('.l-dialog-detachable').length == 0
        @closePoint() if ev.keyCode == 27 && dialog_not_open

      current_user = App.request 'user:current'

      if current_user.canEditPoint @model 
        @trigger 'make_fields_editable'

    makeEditable : ->
      $details_editable = @$el.find('.point_description')
      $details_editable.editable
        resource: 'point'
        pk: @model.id
        url: Routes.proposal_point_path @model.get('long_id'), @model.id
        type: 'textarea'
        name: 'text'
        success : (response, new_value) => @model.set('text', new_value)

      # $details_editable.addClass 'icon-pencil icon-large'
      $details_editable.prepend '<i class="editable-pencil icon-pencil icon-large">'


    closePoint : (go_back) ->
      go_back ?= true
      @trigger 'point:close', go_back

  class Point.PointAvatarArea extends App.Views.ItemView
    template : '#tpl_point_avatar_area'
    className : 'point_avatar_view'

    serializeData : ->
      assessment = @model.getAssessment()
      tenant = App.request 'tenant'
      has_assessment = tenant.get('assessment_enabled') && assessment && assessment.get('complete')

      params = _.extend {}, 
        user : @model.getUser().attributes
        hide_name : @model.get 'hide_name'
        has_assessment : has_assessment

      if has_assessment
        _.extend params, 
          assessment : assessment
          verdict : assessment.getVerdict()
          claims : assessment.getClaims() 
      params

    onRender : ->
      #TODO: in previous scheme, this change was intended to trigger render on point close
      @listenTo @model, 'change', @render

    events : 
      'mouseover .point_assessment_indicator_region' : 'showVerdictTooltip'

    showVerdictTooltip : (ev) ->

      $target = $(ev.currentTarget)
      assessment = @model.getAssessment()
      verdict = assessment.getVerdict()

      template = _.template($('#tpl_verdict_tooltip').html())


      content = switch verdict.id
        when 1
          "Warning: may contain unsubstantiated claims."
        when 2
          "Contains some unverified claims."
        when 3
          "Makes accurate claims."
        else
          # when out of scope, don't show any tooltip
          null


      if content
        html = template
          content : content

        $target.tooltipster
          content: $(html)
          offsetY: -12
          delay: 200
          position: 'top-left'
          #title : 'This point has been fact checked'

        $target.tooltipster 'show'


  class Point.PointSummaryView extends App.Views.ItemView
    template : '#tpl_point_summary'
    className : 'point_summary_view'

    serializeData : ->
      params = _.extend {}, @model.attributes, 
        adjusted_nutshell : @model.adjusted_nutshell()
        user : @model.getUser().attributes
        proposal : @model.getProposal().attributes
        actions : @options.actions
      
      params

    onShow : ->
      @listenTo @model, 'change', @render      

    onRender : ->
      @stickit()

      current_user = App.request 'user:current'
      if @$el.parents('.open_point').length > 0 && current_user.canEditPoint @model 
        @makeEditable()

    bindings : 
      '.open_point_link' : 
        observe : 'comment_count'
        onGet : -> 
          if @model.get('comment_count') == 1 then "1 comment" else "#{@model.get('comment_count')} comments"


    makeEditable : ->
      $editable = @$el.find('.point_nutshell')
      $editable.editable
        resource: 'point'
        pk: @model.id
        url: Routes.proposal_point_path @model.get('long_id'), @model.id
        type: 'textarea'
        name: 'nutshell'
        success : (response, new_value) => @model.set('nutshell', new_value)

      # $editable.addClass 'icon-pencil icon-large'

      $editable.prepend '<i class="editable-pencil icon-pencil icon-large">'

    removeEditable : ->
      $editable = @$el.find('.point_nutshell')
      $details_editable = @$el.find('.point_description')

      $editable.editable('destroy')
      $details_editable.editable('destroy')
      # $editable.removeClass 'icon-pencil icon-large'
      # $details_editable.removeClass 'icon-pencil icon-large'

      @$el.find('.editable-pencil').remove()




  class Point.FollowPointView extends App.Views.ItemView
    template : '#tpl_follow_point'
    className : 'follow_point_view'

    serializeData : ->
      current_user = App.request 'user:current'
      params = _.extend {}, @model.attributes,
        already_follows : current_user.isFollowing 'Point', @model.id
        current_user_id : current_user.id
      params

    onShow : ->
      current_user = App.request 'user:current'
      @listenTo current_user, "follow:changed:Point#{@model.id}", ->
        @render()

    events : 
      'click .point-follow' : 'toggleFollow'

    toggleFollow : (ev) ->
      @trigger 'point:follow'

